plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = 'TerraformersMC'
                url = 'https://maven.terraformersmc.com/'
            }
        }
        filter {
            includeGroup("com.terraformersmc")
            includeGroup("dev.emi")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Ladysnake Mods'
                url = 'https://maven.ladysnake.org/releases'
            }
        }
        filter {
            includeGroup("org.ladysnake.cardinal-components-api")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Fabric-ASM and other GitHub repos'
                url = "https://jitpack.io/"
            }
        }
        filter {
            includeGroup("com.github.Chocohead")
        }
    }
}

configurations {
    implementation.extendsFrom library
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.mrcrayfish:framework-fabric:${minecraft_version}-${framework_version}"
    modCompileOnlyApi "com.terraformersmc:modmenu:${modmenu_version}"
    modImplementation "dev.emi:trinkets:${trinkets_version}"
    modRuntimeOnly "com.mrcrayfish:catalogue-fabric:${minecraft_version}-${catalogue_version}"
}

sourceSets.main.resources.srcDirs += "src/generated/resources"

fabricApi {
    configureDataGeneration()
}

loom {
    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}
