import net.minecraftforge.gradle.common.tasks.SignJar
import org.apache.commons.io.FilenameUtils

plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

// Fixes an issue where net.sf.jopt-simple:jopt-simple cannot be resolved
configurations.configureEach {
    resolutionStrategy {
        it.force 'net.sf.jopt-simple:jopt-simple:5.0.4'
    }
}

mixin {
    config "${mod_id}.forge.mixins.json"
    config "${mod_id}.common.mixins.json"
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    reobf = false
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    /*def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = at
    }*/

    runs {
        configureEach {
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'forge.logging.markers', 'REGISTRIES,SCAN,LOADING'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
        }
        client {
            workingDirectory project.file('runs/client')
            property 'forge.enabledGameTestNamespaces', mod_id
        }
        server {
            workingDirectory project.file('runs/server')
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }
        data {
            workingDirectory project.file('runs/data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        name = 'Curios API'
        url = 'https://maven.theillusivec4.top/'
    }
}

configurations {
    implementation.extendsFrom library
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation "com.mrcrayfish:framework-forge:${minecraft_version}-${framework_version}"
    implementation "top.theillusivec4.curios:curios-forge:${curios_version}"
    runtimeOnly "com.mrcrayfish:catalogue-forge:${minecraft_version}-${catalogue_version}"
    // Temp hack fix in Forge.
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

    //compileOnly fg.deobf('curse.maven:controllable-317269:4598985')
}

tasks.register('signJar', SignJar) {
    dependsOn jar
    onlyIf {
        hasProperty('keyStore') || System.getenv("KEYSTORE")
    }
    keyStore = findProperty('keyStore') ?: System.getenv("KEYSTORE")
    alias = findProperty('keyStoreAlias') ?: System.getenv("KEYSTORE_ALIAS")
    storePass = findProperty('keyStorePass') ?: System.getenv("KEYSTORE_PASS")
    inputFile = jar.archiveFile
    def name = FilenameUtils.removeExtension(jar.archiveFile.get().getAsFile().getName());
    outputFile = layout.buildDirectory.file("libs/%s-signed.jar".formatted(name));
}

jar.finalizedBy 'signJar'

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}