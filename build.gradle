plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'

    version = "${minecraft_version}-${mod_version}"

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    configurations {
        library
    }
        
    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            name = "MrCrayfish (GitHub)"
            url = "https://maven.pkg.github.com/MrCrayfish/Maven"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_KEY")
            }
            content {
                includeGroup "com.mrcrayfish"
            }
        }
        if (!System.getenv("CI")) {
            maven {
                url "file://" + System.getenv("LOCAL_MAVEN")
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release.set(17)
    }

    processResources {
        def expandProps = [
                "group": project.group,
                "minecraft_version": minecraft_version,
                "forge_version": forge_version,
                "fabric_version": fabric_version,
                "fabric_loader_version": fabric_loader_version,
                "neoforge_version": neoforge_version,
                "neoforge_version_range": neoforge_version_range,
                "neoforge_loader_version_range": neoforge_loader_version_range,
                "framework_version": framework_version,
                "catalogue_version": catalogue_version,
                "curios_version": curios_version,
                "trinkets_version": trinkets_version,
                "mod_name": mod_name,
                "mod_version": mod_version,
                "mod_author": mod_author,
                "mod_id": mod_id,
                "mod_license": mod_license,
                "mod_desc": mod_desc,
                "mod_homepage": mod_homepage,
                "mod_source": mod_source,
                "mod_issues": mod_issues
        ]
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    // Mutes javadoc warnings
    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

tasks.register('incrementBuild') {
    doLast {
        int version = file('/release_build').text.toInteger()
        file('/release_build').text = version + 1
    }
}
